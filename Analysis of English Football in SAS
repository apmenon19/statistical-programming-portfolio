filename BR '/home/u63988357/STAT5740HW/SAS Final Project/BR-Football-Dataset.csv';
filename ENGLAND '/home/u63988357/STAT5740HW/SAS Final Project/ENGLAND-Football-Dataset.csv';

/* Create combined dataset from  football data */
data WORK.combined_soccer_data;
    /* Define maximum length for string variables */
    length tournament home away time date ht_result at_result dataset $50.;
    
    /* Read data from football CSV file */
    infile BR firstobs=2 delimiter=',' dsd missover;
    
    /* Input variables from the CSV file */
    input tournament $ home $ home_goal away_goal away $ home_corner away_corner
          home_attack away_attack home_shots away_shots time $ date $ 
          ht_diff at_diff ht_result $ at_result $ total_corners;
    
    /* Add dataset identifier */
    dataset = "BR Football Data";
run;

/* Question 1 - Analyze Home Advantage in Goal Scoring */
data WORK.analyze_home_advantage;
    set WORK.combined_soccer_data;
    /* Determine if home team scored more goals */
    if home_goal > away_goal then home_advantage = "Yes";
    else home_advantage = "No";
run;

/* Visualize home advantage with a bar plot */
proc sgplot data=WORK.analyze_home_advantage;
    vbar home_advantage / response=home_goal group=home_advantage 
                         stat=freq datalabel dataskin=pressed;
    xaxis label="Home Team Scoring More Goals";
    yaxis label="Number of Matches";
    title "Does Playing at Home Increase the Likelihood of Scoring More Goals?";
run;

/* Conduct paired t-test to compare home and away goals */
proc ttest data=WORK.combined_soccer_data;
    paired home_goal*away_goal;
    title "Paired T-Test: Home Goals vs. Away Goals";
run;

/* Question 2 - Analyze Shots by Time of Day */
data WORK.shots_by_hour;
    set WORK.combined_soccer_data;
    /* Extract hour from time and calculate total shots */
    hour = input(scan(time, 1, ':'), 8.);
    total_shots = home_shots + away_shots;
run;

/* Visualize average shots by hour */
proc sgplot data=WORK.shots_by_hour;
    vbar hour / response=total_shots stat=mean datalabel;
    xaxis label="Hour of Match Start";
    yaxis label="Average Total Shots";
    title "Average Total Shots by Hour of Match Start";
run;

/* Perform ANOVA to test if match start hour affects total shots */
proc glm data=WORK.shots_by_hour;
    class hour; 
    model total_shots = hour; 
    means hour / tukey cldiff; 
    title "ANOVA: Effect of Match Start Hour on Total Shots";
run;

/* Question 3 - Analyze Relationship Between Corners and Goals */
data WORK.corners_goals_relation;
    set WORK.combined_soccer_data;
    /* Calculate total goals in each match */
    total_goals = home_goal + away_goal;
run;

/* Create scatter plot of corners vs. goals */
proc sgplot data=WORK.corners_goals_relation;
    scatter x=total_corners y=total_goals / markerattrs=(symbol=circlefilled color=blue);
    xaxis label="Total Corners";
    yaxis label="Total Goals Scored";
    title "Relationship Between Total Corners and Total Goals Scored";
run;

/* Compute correlation between corners and goals */
proc corr data=WORK.corners_goals_relation pearson spearman;
    var total_corners total_goals;
    title "Correlation Analysis: Total Corners vs. Total Goals";
run;

/* Question 4 - Analyze Halftime Leading and Full-Time Result */
/* Create frequency table and chi-square test */
proc freq data=WORK.combined_soccer_data;
    tables ht_result*at_result / chisq out=WORK.ht_at_result_count;
    title "Chi-Square Test: Halftime Result vs. Full-Time Outcome";
run;

/* Visualize halftime and full-time result relationship */
proc sgplot data=WORK.ht_at_result_count;
    vbar ht_result / response=COUNT stat=sum group=at_result groupdisplay=cluster datalabel;
    xaxis label="Halftime Result (Home or Away)";
    yaxis label="Count of Matches";
    title "Likelihood of Home Team Leading at Halftime to Win at Full-time";
run;
